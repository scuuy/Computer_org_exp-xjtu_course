# <center>实验三 算术逻辑单元设计

### **一、实验目的**

1 掌握Verilog语言和Vivado、Logisim开发平台的使用；

2 掌握算术逻辑单元的设计和测试方法。

 

### **二、实验内容**

1 运算模块的设计与测试

2 算术逻辑单元设计与测试

 

### **三、实验要求**

1 掌握Vivado或Logisim开发工具的使用，掌握以上电路的设计和测试方法；

2 记录设计和调试过程（Verilog代码/电路图/表达式/真值表，Vivado仿真结果，Logisim验证结果等）；

3 分析Vivado仿真波形/Logism验证结果，注重输入输出之间的对应关系。

 

### **四、实验过程及分析**

本次实验完成32位运算逻辑单元ALU的设计与测试，包括各运算模块的设计

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104160619867.png" alt="image-20231104160619867" style="zoom:70%;" />

功能如表所示

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104160737540.png" alt="image-20231104160737540" style="zoom:50%;" />

 *实验使用vivado*

1. **行为描述方式**

design代码

```verilog
module ALU(OP,A,B,F,ZF,CF,OF,SF,PF);
	parameter SIZE=32;   //运算位数
    input [3:0]OP;		//运算操作
    input [SIZE:1]A;	//左运算数
    input [SIZE:1]B;	//右运算数
    output [SIZE:1]F;	//结果
	output  ZF,			//0标志位，运算结果全0则置1，否则0
  	   		CF,			//进位借位标志，有进位或借位为1
  	 		OF,			//溢出标志位，对有符号数，溢出为1
   			SF,			//符号标志位，同F最高位
  			PF;			//奇偶标志位，F有奇数个1为1
	reg [SIZE:1]F;
	reg C,ZF,CF,OF,SF,PF;	//C为最高位进位
	always@(*)
	begin
		C=0;
		case(OP)
			4'b0000: begin F=A&B; end	//按位与
			4'b0001: begin F=A|B; end	//按位或
			4'b0010: begin F=A^B; end	//按位异或
            4'b0011: begin F=~(A|B); end//按位或非
            4'b0100: begin {C,F}=A+B; end//加法
            4'b0101: begin {C,F}=A-B; end//减法
			4'b0110: begin F=A<B; end		//判断A<B
			4'b0111: begin F=A<<B; end		//B左移A位
		endcase
		ZF=F==0;
		CF=C;
        OF=A[SIZE]^B[SIZE]^F[SIZE]^C;	//溢出标志公式
        SF=F[SIZE];
		PF=~^F;			//奇偶标志，偶数个1时，为1
	end
endmodule
```

- 分析：直接使用行为描述方式，将各个功能以高级语言表达式的方式表示，结构简单，容易实现，参数见注释。
- PF通过运算`~^F`计算，即各位取异或，再取反，事实上PF奇偶标志位与PPT中描述相反，这里当F有偶数个1时PF=1，奇数个1时为0。

sim代码

```verilog
module sim_ALU;
    parameter SIZE=32;
    reg [3:0]OP;
    reg [SIZE:1] A,B;
    wire [SIZE:1] F;
    wire ZF,CF,OF,SF,PF;
    ALU alu(OP,A,B,F,ZF,CF,OF,SF,PF);
    initial begin
    OP=0;A=0;B=0;
    fork
        repeat(1024)#1 A=A+1;
        repeat(1024)#2 B=B+1;
        repeat(100)#10 OP=(OP+'b1)%('b1000);
    join
    end 
endmodule
```

- 分析：使用initial begin语句，fork出3个不同频率脉冲的信号进行仿真，OP取模循环枚举。

simulation:

![image-20231104163429558](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104163429558.png)

光标处OP=0执行与操作，A='b101, B='b010 ,`F=101&010=0,ZF=0`,PF=1,说明是偶数(0)个1，符合预期.

RTL分析

![image-20231104165146967](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104165146967.png)



2. **三种描述方式综合实现**

design文件

```verilog
module ALU_8(F,CF,A,B,OP);
	parameter SIZE=32;
	output reg [SIZE-1:0]F;
	output CF;
	input [SIZE-1:0]A,B;
	input [3:0]OP;
    //功能定义
	parameter ALU_AND =4'B0000;
	parameter ALU_OR =4'B0001;
	parameter ALU_XOR =4'B0010;
	parameter ALU_NOR =4'B0011;
	parameter ALU_ADD =4'B0100;
	parameter ALU_SUB =4'B0101;
	parameter ALU_SLT =4'B0110;
	parameter ALU_SLL =4'B0111;
	wire [7:0]EN;
	wire [SIZE-1:0]Fw,Fa;
	assign Fa=A&B;
	always@(*)begin
	case(OP)
		ALU_AND :begin F<=Fa; end
		ALU_OR :begin F<=A|B; end
		ALU_XOR :begin F<=A^B; end
		ALU_NOR :begin F<=~(A|B); end
		default: F=Fw;
	endcase
end
decoder38 decoer38_1(OP[2:0],EN);
add32 add32(A,B,'b0,Fw,CF,EN[4]);
SUB sub_1(Fw,CF,A,B,EN[5]);
SLT slt_1(Fw,A,B,EN[6]);
SLL sll_1(Fw,A,B,EN[7]);
endmodule
```

- 分析：对于基本的4种按位操作，直接使用行为描述方式实现（case语句）。对于其他4种运算，通过调用子模块的方式实现，基本思路是，通过3-8译码器，将OP的3位信号译码，此时对于调用不同功能，仅有1位高有效，例如功能5，则Y5=1,此时向功能5子模块传入使能信号EN[5]，其余同理。这样可以保证，在一种运算发生时候，保证使能有效，其余子模块均无效，输出高阻态，避免了产生总线访问冲突。

以下分别设计译码器以及4个子模块

**2.1 3-8译码器**

原理不再赘述，design代码

```verilog
module decoder38(A,Y);
	input [2:0]A;
	output reg [7:0]Y;
	always@(*)begin
	case(A)
		3'b000:begin Y=8'b0000_0001;end
		3'b001:begin Y=8'b0000_0010;end
		3'b010:begin Y=8'b0000_0100;end
		3'b011:begin Y=8'b0000_1000;end
		3'b100:begin Y=8'b0001_0000;end
		3'b101:begin Y=8'b0010_0000;end
		3'b110:begin Y=8'b0100_0000;end
		3'b111:begin Y=8'b1000_0000;end
		default : Y=8'b0000_0000;
	endcase
	end
endmodule
```

**2.2 算数加法器**

思路：通过半加器，一位全加器，4位全加器，32位(8*4)位全加器的方式逐层实现

半加器(后文均为design代码)

```verilog
module half_add(input A,input B,output S,output C);
    assign S=A^B;
    assign C=A&B;
endmodule
```

全加器

```verilog
module add1(
input A,input B,input Ci,
output S,output C);
    wire S1,C1,C2;
    //assign {C,S}=A+B+Ci;
   half_add half_add1(A,B,S1,C1);
    half_add half_add2(S1,Ci,S,C2);
    assign C=C1|C2;
endmodule
```

事实上也可以用注释的行为方式实现，这里只是为了调用半加器。

4位全加器

```verilog
module add4(A,B,Ci,S,C);
input [3:0] A,B;
input Ci;
output reg [3:0]S;
output reg C;
wire C1,C2,C3,CC;
wire [3:0]SS;
add1 add4_1(A[0],B[0],Ci,SS[0],C1);
add1 add4_2(A[1],B[1],C1,SS[1],C2);
add1 add4_3(A[2],B[2],C2,SS[2],C3);
add1 add4_4(A[3],B[3],C3,SS[3],CC);
always@(*)begin
    S<=SS;
    C<=CC;
end
endmodule
```

32位全加器

```verilog
module add32(A,B,Ci,S,C,EN);
input [31:0]A,B;
input Ci,EN;
output reg [31:0]S;
output reg C;
wire C1,C2,C3,C4,C5,C6,C7,Ct;
wire [31:0] St;
add4 add0_3(A[3:0],B[3:0],Ci,St[3:0],C1);
add4 add4_7(A[7:4],B[7:4],C1,St[7:4],C2);
add4 add8_11(A[11:8],B[11:8],C2,St[11:8],C3);
add4 add12_15(A[15:12],B[15:12],C3,St[15:12],C4);
add4 add16_19(A[19:16],B[19:16],C4,St[19:16],C5);
add4 add20_23(A[23:20],B[23:20],C5,St[23:20],C6);
add4 add24_27(A[27:24],B[27:24],C6,St[27:24],C7);
add4 add28_31(A[31:28],B[31:28],C7,St[31:28],Ct);
always@(*)begin //debug
    if(~EN)begin
     S=32'bz;
     C=32'bz;
     end
     else begin
     S=St;
     C=Ct;
     end
end
endmodule
```

- 分析：ALU模块直接调用的是32位全加器，因此此处要有非使能情况的高阻态，（最开始设计时，在always中添加了Ci，导致所有输出均有延迟，debug会在后文中描述，此处不赘述。)
- 通过调用8个4位全加器（4个4位实现16位再到32位同理，只是不想再写一个design文件了），实现32位全加器，定义中间进位信号C1-C7,实现每4位向高位的进位。

单独对32位加法器进行验证，sim文件如下

```verilog
module sim_add;
parameter SIZE=31;
reg [SIZE:0]A,B;
reg Ci,EN;
wire [SIZE:0]S;
wire C;
add32 add32(A,B,'b0,S,C,1);
    initial begin
    A=0;B=0;Ci=0;EN=1;
    fork
        repeat(100)#8 A=A+1;
        repeat(100)#5 B=B+1;
    join
    end
endmodule
```

赋上级进位信号为0，simulation如下

![image-20231104172206146](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104172206146.png)

符合预期。

**2.3 算数减法器**

```verilog
module SUB(F,CF,A,B,EN);
	input [31:0]A;
	input [31:0]B;
	input EN;
	output [31:0]F;
	output CF;
	reg [31:0]F;
	reg CF;
	always@(*)begin
	case(EN)
	1:begin F<=A-B;
		CF<=A>=B?1'b0:1'b1;
	end
	0:begin F<=32'bz;
		CF<=1'bz;
	end
	endcase
end
endmodule
```

- 分析：EN=0则赋值高阻态，EN=1时，阻塞赋值A-B,是否借位只需判断A和B的大小即可。

**2.4 比较器**

```verilog
module SLT(F,A,B,EN);
	input [31:1]A;
	input [31:0]B;
	input EN;
	output [31:0]F;
	reg [31:0]F;
	always@(A,B,EN)
	begin
	if (EN)begin
        if(A<B)F<={{7{4'b0000}},4'b0001};
        else F<={8{4'b0000}};
	end
	else F<=32'bz;	
end
endmodule
```

- 使能同理，有效时根据A和B大小为输出F赋值，注意这里F是32位数据，不能直接赋值0或1，因此要补全32位数据，直接使用拼接式赋值。

**2.5 逻辑左移**

```verilog
module SLL(F,A,B,EN);
	parameter N=32;
	output reg[N-1:0]F;
	input [N-1:0]A,B;
	input EN;
	always@(A,B,EN)begin
		if(EN==1)F<=B<<A;
		else F<=32'bz;
	end
endmodule
```

至此所有功能子模块已实现，ALU8的sim文件如下

```verilog
module sim_ALU_8;
    parameter SIZE=32;
    reg [3:0]OP;
    reg [SIZE:1] A,B;
    wire [SIZE:1] F;
    wire CF;
    ALU_8 alu(F,CF,A,B,OP);
    initial begin
    OP=0;A={{7{4'b1111}},4'b0000};B=0;
    fork
        repeat(2048)#5 A=A+1;
        repeat(1024)#8 B=B+1;
        repeat(4096)#3 OP=(OP+'b1)%('b1000);
    join
    end 
endmodule
```

这里为了使运算能达到32位进位，设置A初始值为2^32-1-15,可以很快达到进位的波形。

各功能simulation如下

1. **OP=4,加法器**

![image-20231104180433318](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104180433318.png)

A=18, B=19, 理论和为31，符合预期，CF=0,没有进位

![image-20231104181104699](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104181104699.png)

A=ffff_fffc, B=7, 理论和为 1_0000_0003H, 此时超过32位，有进位信号CF，符合预期。

2. **OP=5,减法器**

![image-20231104181737640](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104181737640.png)

A=b3(H), B=7a(H), 理论差为 39，A>B, 没有借位，CF=0,符合预期。

![image-20231104181655635](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104181655635.png)

A=1, B=b, A<B,因此需要借位，CF=1,借位后差值为`1_0000_0001(H)-0000_000b(H)=ffff_fff6`，观察F结果，符合预期。

3. **OP=6,比较器(A<B)**

![image-20231104182356880](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104182356880.png)

A=b3(H), B=7a(H), A>B ,故F=0,符合预期。

![image-20231104182535541](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104182535541.png)

A=7, B=e, A<B, F=1, 符合预期。

4. **OP=7,逻辑左移（B<<A)**

![image-20231104182722965](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104182722965.png)

A=7, B=e, F理论值为e<<7,即`14*（2^7)(D)=1792(D) = 700（H）`，符合预期。

由于逻辑左移没有设置进位信号，因此进位部分直接丢弃。

ALU的RTL分析如下

![image-20231104194551543](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231104194551543.png)



### **五、调试和心得体会**

####  调试问题

1. 变量类型

​	在编写32位加法器子模块的design文件时，遇到一个互相矛盾的bug，当我想要实例化4位加法器，必须将C,S定义成wire类型，与此同时，我必须在always中对reg进行赋值。解决方法是，引入中间变量Ct和St，此时Ct,St作为实例化的实参带入。在always中，将Ct,St赋值给C,S，由此同时满足两处的变量类型。

2. always@（）触发沿变量问题

​	同样是在32位加法器中的问题，这次的代码事实上可以写成always@(A,B,EN)的形式，由于我设计的是带上级进位的加法器，因此我将上一级进位Ci信号放入，即always@(A,B,Ci,EN),我在调用时实例化时直接传0，理论上我在A,B变化时，应该立即执行我的赋值，即F的值会立即随之变化，然而通过仿真后发现，所有的结果都少1，真值会在一定的延迟后出现（在机房忘记截图了），修改成always@(*)又没有这个问题了，因此我猜测是常量作为敏感变量会有一些特定的机制或者bug，上网查询无果，姑且认为是bug。

3. 先行进位加法器

​	实现并行加法器的本质就是通过如下递推公式消除逐级传递的中间进位信号Ci：
$$
C_{i+1} = A_iB_i + C_i(A_i + B_i)
$$
将Ci以同样的方式展开为Ci-1的形式带入，直到C0，从而达到每一级之间的进位表达式不需要串行传递的目的。

#### 心得体会

​	通过本次实验，学习了ALU算术逻辑单元的设计与测试，使用verilog语言编写了各模块的设计与仿真，并在时序图中得到验证，学习了分层设计硬件从而实现不同子功能的设计思路。同时在编写代码过程中遇到了一些新的问题，对verilog有更深的领悟，学习了总线类型的输出信号用三态门控制等设计方案。