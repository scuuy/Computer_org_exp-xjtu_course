## <center>实验五 数据通路与控制单元设计

### **一、实验目的**

1 掌握Verilog语言和Vivado、Logisim开发平台的使用；

2 掌握数据通路与控制单元的设计和测试方法。

 

### **二、实验内容**

1 数据通路的设计；

2 控制单元的设计。

 

### **三、实验要求**

1 掌握Vivado或Logisim开发工具的使用，掌握以上电路的设计和测试方法；

2 记录设计和调试过程（Verilog代码/电路图/表达式/真值表，Vivado仿真结果，Logisim验证结果等）；

3 分析Vivado仿真波形/Logism验证结果，注重输入输出之间的对应关系。

 

### **四、实验过程及分析**

 实验共有3个design文件，分别是主译码器、ALU译码器和宏观控制模块,结构如图

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120152916115.png" alt="image-20231120152916115" style="zoom:50%;" />

#### 1.主译码器

端口定义如下

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120152850354.png" alt="image-20231120152850354" style="zoom:67%;" />

译码表如下

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120153951387.png" alt="image-20231120153951387" style="zoom:67%;" />

design代码如下

```verilog
module MainDec(
	input [5:0] Op,
	output MemToReg, MemWrite,
	output Branch, ALUSrc,
	output RegDst, RegWrite,
	output Jump,
	output [1:0] ALUOp);
	reg[8:0] Controls;
	assign{RegWrite,RegDst,ALUSrc,Branch,MemWrite,MemToReg,Jump,ALUOp}=Controls;
	always@(*)
		case(Op)
			6'b000000: Controls <=9'b110000010;// RTYPE
			6'b100011: Controls<=9'b101001000;//LW
			6'b101011: Controls <=9'b001010000;//SW
			6'b000100: Controls <=9'b000100001;//BEQ
			6'b001000: Controls<=9'b101000000;//ADDI
			6'b000010: Controls <=9'b000000100;//J
			default: Controls<=9'bxxxxxxxxx;//illegal Op
		endcase
endmodule
```

- assign利用拼接赋值将Controls一次分别对各控制信号赋值。
- case根据Op指令码decode后存入Controls，遇到预料外的Op指令则默认全x（未知状态）。

RTL电路如下

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120153805251.png" alt="image-20231120153805251" style="zoom:67%;" />

#### 2.ALU译码器

端口定义如下

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120153901569.png" alt="image-20231120153901569" style="zoom:50%;" />

ALU译码表如下

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120154022171.png" alt="image-20231120154022171" style="zoom:67%;" />

design代码如下

```verilog
module ALUDec(
	input [5:0] Funct,
	input [1:0] ALUOp,
	output reg[2:0] ALUControl);
	always@(*)
		case(ALUOp)
			2'b00: ALUControl<=3'b010;// add(for lw/sw/addi)
			2'b01: ALUControl <=3'b110;//sub(for beq)
			default: case(Funct)     //R-type Instructions
				6'b100000: ALUControl<=3'b010; //add
				6'b100010: ALUControl <=3'b110;//sub
				6'b100100: ALUControl <=3'b000;//and
				6'b100101: ALUControl<=3'b001;//or
				6'b101010: ALUControl<=3'b111;//slt
				default:  ALUControl<=3'bxxx;//???
				endcase
		endcase
endmodule
```

- 再always中根据ALUOp取值对ALUControl赋值，在default处嵌套一个case语句，即根据Funct再判断，对ALUControl赋值。默认取值xxx（未知状态）。

RTL电路如下

![image-20231120154238998](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120154238998.png)

#### 3.宏观控制器

design代码如下

```verilog
module Controller(
	input [5:0] Op,Funct,
	input Zero,
	output MemToReg, MemWrite,
	output PCSrc,ALUSrc,
	output RegDst,RegWrite,
	output Jump,
	output [2:0] ALUControl);
	wire[1:0] ALUOp;
	wire Branch;
	MainDec mainDec(Op,MemToReg,MemWrite,Branch,ALUSrc,RegDst,RegWrite,Jump,ALUOp);
	ALUDec aludec(Funct,ALUOp,ALUControl);
	assign PCSrc=Branch & Zero;
endmodule
```

- 在实现上，通过控制模块调用主译码器和ALU译码器，实现两个模块独立且协同工作。

- `PCSrc=Branch&Zero`这一行是用来决定程序计数器（PC）的下一个值的。这是在实现条件分支指令的功能。

  具体来说，Branch和Zero都是控制信号。Branch信号是由控制单元生成的，当执行分支指令（如beq）时，Branch会被设置为1。Zero信号则是由算术逻辑单元（ALU）生成的，当ALU的两个输入相等时，Zero会被设置为1。

  因此，当Branch和Zero都为1时，PCSrc会被设置为1。这意味着，如果正在执行的是分支指令，并且分支条件满足（即ALU的两个输入相等），那么PC的下一个值将会是分支目标地址。否则，PC的下一个值将会是PC+4，也就是顺序执行下一条指令。

  这是实现条件分支指令的一种常见方法，可以有效地控制程序的流程。

sim代码如下

```verilog
module sim_Controller;
  reg [5:0] Op;
  reg [5:0] Funct;
  reg Zero;
  wire MemToReg, MemWrite;
  wire PCSrc, ALUSrc;
  wire RegDst, RegWrite;
  wire Jump;
  wire [2:0] ALUControl;
  Controller controller (Op,Funct,Zero,MemToReg,MemWrite,PCSrc,ALUSrc,RegDst,RegWrite,Jump,ALUControl);
  initial begin
    Op = 6'b000000;Funct = 6'b000000;Zero = 'b0;
    fork
      repeat(100) #10 Op = (Op + 1)%'b1000000;
      repeat(100) #8 Funct = (Funct + 1)%'b1000000;
      repeat(100) #25 Zero = ~Zero;
    join
  end
endmodule
```

- 对Op信号和Funct信号进行取模枚举即可，同时也对ZERO不断执行取反操作。
- 由于是+1枚举，因此必然会出现意料外的Op信号，对于这些信号我们已经将其置为X，接下来运行仿真如图

![image-20231120152451609](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120152451609.png)

光标处`Op='b10`,因此`j=1`且`ALUOp=00`,`ALUCtrl='b010`,符合预期。

![image-20231120153305691](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120153305691.png)

光标处`Op=16H`是非法操作码，`ALUOp=x`，ALU模块将根据Funct判定ALUControl信号，此时`Funct=24H=36D='b100100`，此时是R-type指令中的and操作，ALUCtrl信号为0，逻辑上有错误（后文分析），但符合代码预期。

 全局的RTL分析如下

![image-20231120160141954](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120160141954.png)

`MainDec`模块根据输入的操作码`Op`生成一组控制信号，这些控制信号用于控制数据路径中的不同部分。例如，`MemToReg`，`MemWrite`，`Branch`，`ALUSrc`，`RegDst`，`RegWrite`，`Jump`等都是由`MainDec`生成的控制信号。

`ALUDec`模块则根据输入的功能码`Funct`和`ALUOp`生成`ALUControl`信号，这个信号用于控制ALU的操作。

这两个模块的输出信号并不直接相互影响，但它们都是为了控制数据路径中的操作而生成的。也就是说，`MainDec`的输出信号并不直接影响`ALUDec`的输出，反之亦然。然而，它们的输出信号会共同决定数据路径中的操作，因此可以说它们是协同工作的。

### **五、调试和心得体会**

#####  1.调试

​	在ALUDec模块代码中，对于ALUOp=x的判断被错误的加入到default语句中，(ALU译码表如下)

<img src="C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120160601488.png" alt="image-20231120160601488" style="zoom:50%;" />

部分代码语句如下

```verilog
case(ALUOp)
			2'b00: ALUControl<=3'b010;// add(for lw/sw/addi)
			2'b01: ALUControl <=3'b110;//sub(for beq)
			default: case(Funct)
                //......
```

事实上，我们只是期望的default仅仅是`ALUOp='b10`,但在仿真波形中可以观察到x被当作和'b10同等低位处理，都进入了dedault语句，因此我们不妨还是将'b10作为一个 条件写入case的判断语句，修改如下(部分)

```verilog
case(ALUOp)
			2'b00: ALUControl<=3'b010;// add(for lw/sw/addi)
			2'b01: ALUControl <=3'b110;//sub(for beq)
			2'b10: case(Funct)
                6'b100000: ALUControl<=3'b010; //add
				6'b100010: ALUControl <=3'b110;//sub
				6'b100100: ALUControl <=3'b000;//and
				6'b100101: ALUControl<=3'b001;//or
				6'b101010: ALUControl<=3'b111;//slt
				default:  ALUControl<=3'bxxx;//???
				endcase
            default:ALUControl <=3'bxxx;
endcase
```

这样逻辑上主模块向ALU发出全x信号时，ALU同样清楚，这是错误的指令码，将控制信号置为全x。

重新运行仿真如下

![image-20231120161347855](C:\Users\ROG\AppData\Roaming\Typora\typora-user-images\image-20231120161347855.png)

可以观察到，同样的位置下，ALU控制信号变为全x，符合预期。



##### 2.心得体会

​	这次的实验让我深入理解了数据通路和控制单元的设计原理，同时也掌握了Verilog语言和开发平台的使用方法。在实验中，我主要涉及了主译码器、ALU译码器和宏观控制器的设计，这三个模块共同构成了计算机系统中的控制单元和数据通路。

​	首先，主译码器在整个设计中起到了关键的作用。通过对操作码(Op)的解析，主译码器生成了一系列控制信号，如MemToReg、MemWrite、Branch、ALUSrc、RegDst、RegWrite和Jump等。这些信号决定了数据通路中各个部分的工作状态，从而实现了对不同指令的有效控制。

​	其次，ALU译码器负责解析功能码(Funct)和ALUOp，生成ALU控制信号(ALUControl)，该信号用于控制算术逻辑单元(ALU)的操作。这一模块的设计使得计算机系统能够执行不同类型的运算，包括加法、减法、逻辑与或等。

​	最后，宏观控制器通过整合主译码器和ALU译码器，形成了对整个数据通路和控制单元的控制。该模块的设计实现了对数据通路中各个部分协同工作的功能，包括条件分支的处理和程序计数器(PC)的控制。

​	在调试过程中，我注意到了一个关键的问题，即ALU译码器中对于ALUOp的判断应该更为精细，以避免不同信号被错误地归为同一个条件。通过修改代码，我成功解决了这个问题，确保了整个系统的正确性。

​	总的来说，通过这次实验，我不仅学到了如何使用Verilog语言进行硬件描述，还深入理解了数据通路和控制单元的设计原理。这对于理解计算机系统的工作方式和进行数字电路设计都是非常有益的经验。